---
title: Router
description: "Details about inbuilt Router for React in Rescript"
canonical: "/docs/react/latest/router"
---

# Router


React comes with a router in Rescript! We've leveraged the language and library features in order to create a router that's:

- The simplest, thinnest possible.
- Easily pluggable anywhere into your existing code.
- Performant and tiny.

Here's the public interface:

- `RescriptReactRouter.push(string)`: takes a new path and update the URL.
- `RescriptReactRouter.replace(string)`: like `push`, but replaces the current URL.
- `RescriptReactRouter.watchUrl(f)`: start watching for URL changes. Returns a subscription token. Upon url change, calls the callback and passes it the `RescriptReactRouter.url` record.
- `RescriptReactRouter.unwatchUrl(watcherID)`: stop watching for url changes.
- `RescriptReactRouter.dangerouslyGetInitialUrl()`: get `url` record outside of `watchUrl`. Described later.
- `RescriptReactRouter.useUrl(~serverUrl)`: get access to the current url as a hook in your React component. No need to setup `watch`, `unwatch`, and `dangerouslyGetInitialUrl` yourself.

## Match a Route

**There's no API**! `watchUrl` gives you back a `url` record of the following shape:

```res
type url = {
  /* path takes window.location.pathname, like "/book/title/edit" and turns it into `["book", "title", "edit"]` */
  path: list<string>,
  /* the url's hash, if any. The # symbol is stripped out for you */
  hash: string,
  /* the url's query params, if any. The ? symbol is stripped out for you */
  search: string
}
```

So the url `www.hello.com/book/10/edit?name=Jane#author` is given back as:

```res
{
  path: list{"book", "10", "edit"},
  hash: "author",
  search: "name=Jane"
}
```

At this point, you can simply pattern match your way to glory!

```res
let url = RescriptReactRouter.useUrl()
switch (url.path) {
| list{"book", id, "edit"} => handleBookEdit(id)
| list{"book", id} => getBook(id)
| list{"book", id, _} => noSuchBookOperation()
| list{} => showMainPage()
| list{"shop"} | list{"shop", "index"} => showShoppingPage()
| list{"shop", ...rest} =>
    /* e.g. "shop/cart/10", but let "cart/10" be handled by another function */
  nestedMatch(rest)
| _ => showNotFoundPage()
}
```

So you can match a path, match a subset of a path, defer part of a matching to a nested logic, etc.

### Tips & Tricks

Notice that this is just normal [pattern matching](https://rescript-lang.org/docs/manual/latest/pattern-matching-destructuring):

```res
@react.component
let make = () => {
  let url = RescriptReactRouter.useUrl()

  let nowShowing =
    switch (url.hash, MyAppStatus.isUserLoggedIn) {
    | ("active", _) => Active
    | ("completed", _) => Completed
    | ("shared", true) => Shared
    | ("shared", false) when isSpecialUser => /* handle this state please */
    | ("shared", false) => /* handle this state please */
    | _ => All
    }
  /* ... */
}
```

## Push a New Route

From anywhere in your app, just call e.g. `RescriptReactRouter.push("/books/10/edit#validated")`. This will trigger a URL change (without a page refresh) and components with `useUrl`s will be re-rendered.

We might provide better facilities for typed routing + payload carrying in the future!

**Note**: because of browser limitations, changing the URL through JavaScript (aka `pushState`) **cannot** be detected. The solution is to change the URL then fire a `"popState"` event. This is what `Router.push` does, and what the event `useUrl` listens to. So if, for whatever reason (e.g. incremental migration), you want to update the URL outside of `Router.push`, just do `window.dispatchEvent(new Event('popState'))`.
